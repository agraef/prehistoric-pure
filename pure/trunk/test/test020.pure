// math.pure tests by Eddie Rucker

using math;

// unary operations

let f = [sqrt, sin, cos, tan, ln, log, exp, atan, asin, acos, sinh, cosh, tanh,
	 asinh, acosh, atanh, abs, re, im, arg, conj, rect, polar, cis, ceil, 
	 floor, round, frac, complexp, realp, rationalp, numberp, exactp, 
	 inexactp, infp, nanp];
let x = [1, -1, 0, 0.0, 1.2, -1.2, 1%3, -1%4, 1+:2, -1+:2, 1+:-2, -1.2+:4.3,
	 1.2+:-4.3, 1%2+:1, 1%2+:3%4, 3<:1, -3<:1, 3.0<:-3, 3.1<:2.5, (2%3)<:2,
	 (1%2)<:(3%4), -inf, nan];

// binary operations

let f2 = [(+), (-), (*), (/), (^), atan2, pow];
let x2 = [i,j; i=x; j=x];

// test instrumentation (AG)

using system;

/* This will print out each operation, arguments and result as a single
   line. Exceptions are flagged with '__error__', evaluation failures with
   '__failed__'. Floating point values are rounded in the output to 3
   significant digits by default, to prevent 'make check' from failing due to
   rounding discrepancies. */

let double_format = "%#0.3g"; // modify as needed, to get more accuracy

test (f,x,y) = puts $ format (f,x,y,check (f,x,y) (catch __error__ (f x y)));
test (f,x) = puts $ format (f,x,check (f,x) (catch __error__ (f x)));

check _ z = z if numberp z;
check (f,_) (g@_ _ _) |
check (f,_) (g@_ _) = __failed__ if f===g;
check _ z = z otherwise;

format (f,x,y,z) = str f+","+show x+","+show y+","+show z;
format (f,x,z) = str f+","+show x+","+show z;

show x::double = sprintf double_format x;
show (x+:y) = show x+"+:"+show y;
show (x<:y) = show x+"<:"+show y;
show x = str x otherwise;

puts "*** UNARY ***" $$ void [test (f,x); f=f; x=x] $$
puts "*** BINARY ***" $$ void [test (f,x); f=f2; x=x2];
